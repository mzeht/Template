apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'me.tatarka.retrolambda'

def buildTime() {
    return new Date().format("yyyy-MM-dd-HH:mm")
}

def hostName() {
    return System.getProperty("user.name") + "@" + InetAddress.localHost.hostName
}

def propFile = project.rootProject.file('signing.properties');
def Properties props = new Properties();
if (propFile.exists()) {
    props.load(new FileInputStream(propFile));
}
android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.wpmac.template"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        resValue "string", "build_time", buildTime()
        resValue "string", "build_host", hostName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            //设置支持的SO库架构
//            ldLibs.addAll([ 'log']);
            abiFilters 'armeabi','x86'// 'arm64-v8a', 'armeabi-v7a', , 'x86_64'
        }
        sourceSets.main {
            jniLibs.srcDir 'src/main/jinLibs'
        }

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            keyAlias props['RELEASE_KEY_ALIAS']
            keyPassword props['RELEASE_KEY_PASSWORD']
            storeFile file(props['RELEASE_STORE_FILE'])
            storePassword props['RELEASE_STORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
    }
    greendao {
        schemaVersion 1
        targetGenDir 'src/main/java'
    }


    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "mzeht_v${defaultConfig.versionName}_${buildTime()}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // dataBinding 加入
    dataBinding {
        enabled = true
    }


}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$project.ext.supportLibraryVersion"
    compile "com.android.support:design:$project.ext.supportLibraryVersion"
    compile "com.android.support:support-v4:$project.ext.supportLibraryVersion"
    compile "com.android.support:preference-v7:$project.ext.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$project.ext.supportLibraryVersion"
    compile project(':permission')
    compile project(':xrecyclerview')
    compile project(':multiplestatusview')
    compile('me.drakeet.multitype:multitype:3.1.0', {
        exclude group: 'com.android.support'
    })
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:retrofit-converters:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.facebook.stetho:stetho:1.4.2'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    compile 'com.annimon:stream:1.1.3'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'org.projectlombok:lombok:1.16.16'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'com.youth.banner:banner:1.4.2'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'
    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
    compile 'com.tencent.bugly:nativecrashreport:latest.release'
}
